import &ArtSuite

class ChatView extends FluxComponent
  @subscriptions
    history:
      model:  :chatsByChatRoom
      key:    ({chatRoom}) ->  chatRoom

  postMessage: ({target}) ->
    @models.chat.create data: {}
      user:     @props.currentUser
      message:  target.value
      @props.chatRoom
    target.value = ""

  render: ->
    Element
      childrenLayout: :column
      draw: &StyleProps.chatViewBackground

      Element
        size: hch:1
        draw: &Palette.primary

        TextElement
          &StyleProps.titleText
          padding: 10
          text: @props.currentUser

      ScrollElement
        clip: true
        track: :bottom

        Element
          padding:        10
          size:           hch: 1
          childrenLayout: :column

          array postMessage from @history
            &ChatMessage
              postMessage
              {} @props.currentUser

      Element
        size: h: 45
        draw: &Palette.grayBackground

        TextInputElement
          &StyleProps.mediumText
          on: enter: @postMessage
          padding: 10
          placeholder: "" new message from #{@props.currentUser}
