import &ArtSuite

class ChatView extends Component

  history:
    # Some fake history
    {} user: :Alice   message: "" Hi!
    {} user: :Bill    message: "" Hi, Alice!

  render: ->
    {currentUser} = @props

    Element
      padding: 10
      childrenLayout: :column

      RectangleElement inFlow: false &StyleProps.chatViewBackground

      Element
        size: ww:1, hch:1

        RectangleElement color: &StyleProps.palette.primaryBackground

        TextElement &StyleProps.titleText, padding: 10, text: currentUser

      PagingScrollElement
        clip: true

        Element
          padding:        10
          size:           hch: 1, ww: 1
          childrenLayout: :column

          Element inFlow: false, size: 0 # hack ensures first added message animates in

          array postMessage from history
            &ChatMessage
              postMessage
              currentUser: currentUser

      Element
        size: ww: 1, h: 45

        RectangleElement color: &StyleProps.palette.grayBackground

        TextInput
          &StyleProps.mediumText
          padding: 10
          placeholder: "" new message from #{currentUser}
